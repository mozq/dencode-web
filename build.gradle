plugins {
  id 'war'
}

apply from: 'build-minify-plugin.gradle'

def appName = 'dencode'
def appDir = layout.buildDirectory.dir('app')

configurations {
  resolvableCompileOnly {
    extendsFrom(configurations.compileOnly)
    canBeResolved = true
    canBeConsumed = false
  }
}

repositories {
  mavenCentral()
  maven { url = 'https://jitpack.io' }
}

dependencies {
  compileOnly 'org.eclipse.jetty:jetty-server:12.1.+'
  compileOnly 'org.eclipse.jetty:jetty-slf4j-impl:12.1.+'
  compileOnly 'org.eclipse.jetty.ee11:jetty-ee11-webapp:12.1.+'
  compileOnly 'org.eclipse.jetty.ee11:jetty-ee11-annotations:12.1.+'
  compileOnly 'org.eclipse.jetty.ee11:jetty-ee11-apache-jsp:12.1.+'
  
  implementation('org.eclipse.jetty.ee11:jetty-ee11-glassfish-jstl:12.1.+') {
    exclude group: 'org.eclipse.jetty.ee11'
  }
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.+'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.20.+'
  implementation 'com.ezylang:EvalEx:3.5.+'
  
  implementation 'com.github.mozq:enigma4j:v1.+'
  
  testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


java {
  sourceCompatibility = JavaVersion.VERSION_25
  targetCompatibility = JavaVersion.VERSION_25
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

test {
  useJUnitPlatform()
}

minifyJs {
  source 'src/main/webapp/static/js/commons.js'
  source 'src/main/webapp/static/js/analytics.js'
  source 'src/main/webapp/static/js/main.js'
  source 'src/main/webapp/static/js/adsense.js'
  destinationFile = layout.buildDirectory.file('minifyJs/all.min.js')
  languageOut = 'ECMASCRIPT_2020'
}

war {
  archiveBaseName = appName
  from(minifyJs) {
    into 'static/js'
  }
}

tasks.register('archiveServerMainJar', Jar) {
  archiveBaseName = appName
  archiveAppendix = 'server-main'
  
  from sourceSets.main.output
  include 'com/dencode/web/server/**'
  manifest {
    attributes 'Main-Class': 'com.dencode.web.server.ServerMain'
  }
}

tasks.register('packageApp', Copy) {
  from war
  from tasks.named('archiveServerMainJar')
  from configurations.resolvableCompileOnly
  into appDir
}

assemble.configure {
  dependsOn 'packageApp'
}

tasks.register('runApp', Exec) {
  dependsOn assemble
  
  workingDir = appDir
  environment PORT: 8080
  commandLine 'java', '-cp', '*', 'com.dencode.web.server.ServerMain'
}
