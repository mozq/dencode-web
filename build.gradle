plugins {
  id 'war'
}

apply from: 'build-minify-plugin.gradle'

group = 'com.dencode'
version = '1.0.0-SNAPSHOT'

ext {
  appDir = layout.buildDirectory.dir('app')
}

repositories {
  mavenCentral()
  maven { url = 'https://jitpack.io' }
}

dependencies {
  providedCompile 'org.eclipse.jetty:jetty-server:12.1.+'
  providedCompile 'org.eclipse.jetty:jetty-slf4j-impl:12.1.+'
  providedCompile 'org.eclipse.jetty.ee10:jetty-ee10-webapp:12.1.+'
  providedCompile 'org.eclipse.jetty.ee10:jetty-ee10-annotations:12.1.+'
  providedCompile 'org.eclipse.jetty.ee10:jetty-ee10-apache-jsp:12.1.+'
  
  implementation 'org.eclipse.jetty.ee10:jetty-ee10-glassfish-jstl:12.1.+'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.+'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.20.+'
  implementation 'com.ezylang:EvalEx:3.5.+'
  
  implementation 'com.github.mozq:enigma4j:v1.+'
  
  testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


java {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

test {
  useJUnitPlatform()
}

minifyJs {
  source 'src/main/webapp/static/js/commons.js'
  source 'src/main/webapp/static/js/analytics.js'
  source 'src/main/webapp/static/js/main.js'
  source 'src/main/webapp/static/js/adsense.js'
  destinationFile = layout.buildDirectory.file('minifyJs/all.min.js')
  languageOut = 'ECMASCRIPT_2020'
}

war {
  dependsOn minifyJs
  
  webInf {
    from minifyJs.destinationFile
    into "/static/js/"
  }
}

task archiveServerMainJar(type: Jar) {
  archiveAppendix = 'server-main'
  
  from sourceSets.main.output
  include 'com/dencode/web/server/**'
  manifest {
    attributes 'Main-Class': 'com.dencode.web.server.ServerMain'
  }
}

task packageApp(type: Copy) {
  dependsOn assemble
  
  from war.archiveFile
  from archiveServerMainJar.archiveFile
  from configurations.providedCompile
  into appDir
}

assemble.dependsOn archiveServerMainJar
assemble.finalizedBy packageApp

task runApp(type: Exec) {
  dependsOn assemble
  
  environment PORT: 8080
  workingDir appDir.get()
  commandLine 'java', '-cp', '*', 'com.dencode.web.server.ServerMain'
}
