buildscript {
  repositories {
    mavenCentral()
  }
  
  dependencies {
    classpath 'com.google.javascript:closure-compiler:v20251216'
  }
}

import com.google.javascript.jscomp.CompilationLevel
import com.google.javascript.jscomp.Compiler
import com.google.javascript.jscomp.CompilerOptions
import com.google.javascript.jscomp.CompilerOptions.LanguageMode
import com.google.javascript.jscomp.SourceFile
import com.google.javascript.jscomp.WarningLevel
import java.nio.charset.Charset


abstract class MinifyJsTask extends SourceTask {
  @Input abstract Property<String> getCharset()
  @Input abstract Property<String> getCompilationLevel()
  @Input abstract Property<String> getWarningLevel()
  @Input abstract Property<String> getLanguageIn()
  @Input abstract Property<String> getLanguageOut()
  @OutputFile abstract RegularFileProperty getDestinationFile()
  
  MinifyJsTask() {
    charset.convention('UTF-8')
    compilationLevel.convention('SIMPLE_OPTIMIZATIONS')
    warningLevel.convention('DEFAULT')
    languageIn.convention('STABLE')
    languageOut.convention('STABLE')
  }
  
  @TaskAction
  void minify() {
    def options = new CompilerOptions()
    CompilationLevel.fromString(compilationLevel.get()).setOptionsForCompilationLevel(options)
    WarningLevel.valueOf(warningLevel.get()).setOptionsForWarningLevel(options)
    options.setLanguageIn(LanguageMode.fromString(languageIn.get()))
    options.setLanguageOut(LanguageMode.fromString(languageOut.get()))
    
    def compiler = new Compiler()
    def result = compiler.compile([], source.files.collect { SourceFile.fromPath(it.toPath(), Charset.forName(charset.get())) }, options)
    
    result.warnings.each {
      logger.warn('WARN: ' + it.toString())
    }
    
    if (result.success) {
      destinationFile.get().asFile.setText(compiler.toSource(), charset.get())
    } else {
      throw new GradleException('Closure Compiler failed:\n' + result.errors.join('\n'))
    }
  }
}

project.tasks.register('minifyJs', MinifyJsTask)
